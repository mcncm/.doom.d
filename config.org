#+TITLE: Literate Doom Emacs configuration
#+AUTHOR: mcncm
#+PROPERTY: header-args :emacs-lisp :tangle yes :comments no :cache yes :results silent
#+STARTUP: org-startup-folded: showall
Use lexical binding /always/; otherwise, ~let~ constructs will not behave intuitively. Note that for this header to work properly in a literate config, we must include the ~:comments no~ attribute in the headers above, and it must come /after/ ~:tangle yes~. This attribute can also be set for each block.
#+BEGIN_SRC elisp
;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-
#+END_SRC
* Introduction
** Conventions
“Personal” variables and functions have the prefix ~my/~ as a namespacing convention.
* Default Doom config preamble
This is just the block at the top of the ~config.el~ that ships with Doom Emacs, modified to my current tastes. There’s nothing especially interesting here.
#+BEGIN_SRC elisp
;; Place your private configuration here! Remember, you do not need to run 'doom
;; sync' after modifying this file!

;; Some functionality uses this to identify you, e.g. GPG configuration, email
;; clients, file templates and snippets.
(load "~/.doom.d/secrets.el")

;; If you use `org' and don't want your org files in the default location below,
;; change `org-directory'. It must be set before org loads!
(setq org-directory (getenv "ORG_DIR"))

;; Doom exposes five (optional) variables for controlling fonts in Doom. Here
;; are the three important ones:
;;
;; + `doom-font'
;; + `doom-variable-pitch-font'
;; + `doom-big-font' -- used for `doom-big-font-mode'; use this for
;;   presentations or streaming.
;;
;; They all accept either a font-spec, font string ("Input Mono-12"), or xlfd
;; font string. You generally only need these two:
;; (setq doom-font (font-spec :family "monospace" :size 12 :weight 'semi-light)
;;       doom-variable-pitch-font (font-spec :family "sans" :size 13))
(setq my/code-font "Fira Code Retina")
(setq my/code-size 20)
(setq my/text-font "ETBembo")
(setq my/text-size 25)

(setq doom-font (font-spec :family my/code-font :size my/code-size)
      doom-big-font (font-spec :family my/code-font :size 30)
      doom-variable-pitch-font (font-spec :family my/text-font :size my/text-size :weight 'thin))


;; There are two ways to load a theme. Both assume the theme is installed and
;; available. You can either set `doom-theme' or manually load a theme with the
;; `load-theme' function. This is the default:
(setq doom-theme 'ewal-doom-one)

;; This determines the style of line numbers in effect. If set to `nil', line
;; numbers are disabled. For relative line numbers, set this to `relative'.
(setq display-line-numbers-type 'relative)


;; Here are some additional functions/macros that could help you configure Doom:
;;
;; - `load!' for loading external *.el files relative to this one
;; - `use-package!' for configuring packages
;; - `after!' for running code after a package has loaded
;; - `add-load-path!' for adding directories to the `load-path', relative to
;;   this file. Emacs searches the `load-path' when you load packages with
;;   `require' or `use-package'.
;; - `map!' for binding new keys
;;
;; To get information about any of these functions/macros, move the cursor over
;; the highlighted symbol at press 'K' (non-evil users must press 'C-c c k').
;; This will open documentation for it, including demos of how they are used.
;;
;; You can also try 'gd' (or 'C-c c d') to jump to their definition and see how
;; they are implemented.
#+END_SRC
* Generalities
** Some better defaults
I’d like to set some variables that generally make life better.

Here I’m trying to more closely match the [[https://www.spacemacs.org/][Spacemacs]] convention that I like. I'm still not sure what the default “repeat” behavior of the comma key actually /does/. For all I know, I might prefer it. In any case, I should also see if I ought to unmap that key first. Note that this *unmaps* ~SPC-m~.
#+BEGIN_SRC elisp
(setq doom-localleader-key ",")
#+END_SRC

And here are some miscellaneous settings.  All of these are snagged from [[https://tecosaur.github.io/emacs-config/config.html#better-defaults][tecosaur]], who in turn got them from a much larger list of “better defaults” from [[https://github.com/angrybacon/dotemacs/blob/master/dotemacs.org#use-better-defaults][angrybacon]].
#+BEGIN_SRC elisp
(setq-default
 delete-by-moving-to-trash t)

(setq undo-limit 80000000
      truncate-string-ellipsis "…")
#+END_SRC
** Little helper functions
#+BEGIN_SRC elisp
(defun my/find-file-in-home ()
  "Search for a file in home directory."
  (interactive)
  (ido-find-file-in-dir gnus-home-directory))

(setq my/org-agenda-directory (concat org-directory "/agenda"))

(defun my/find-file-in-agenda ()
  "Search for a file in home directory."
  (interactive)
  (ido-find-file-in-dir my/org-agenda-directory))
#+END_SRC

I want these bound to convenient keys to quickly find files in my home directory and agenda.
#+BEGIN_SRC elisp
(map! :map doom-leader-file-map
      "h" #'my/find-file-in-home
      "a" #'my/find-file-in-agenda)
#+END_SRC

Here are two little auxiliary functions for retrieving palette colors. I’m not sure if there’s already a (presumably Doom-specific) function that accomplishes this, but if there is I should use it.
#+BEGIN_SRC elisp
(defun my/ewal-color (color)
  "COLOR should be a symbol defined in ewal-base-palette. Options are:
'comment, 'background, 'foreground, 'cursor, 'black, 'red,
'green, 'yellow, 'blue, 'magenta, 'cyan, 'white"
  (cdr (assoc color ewal-base-palette)))

(defun my/theme-color (color)
  "A generalization of `my/ewal-color` that should with for all Doom themes. COLOR should be a key in `doom-themes--colors`. Returns a hex code."
  (let ((color-spec                 ;; a list with four elements
         (seq-find
         (lambda (x) (eq (car x) color)) ;; take the matching one
         doom-themes--colors)))
    (if color-spec
        (car (cdr color-spec))      ;; return the second elemend
      nil)                          ;; …if it exists.
    ))
#+END_SRC
** Behold the dodecahedron!
This is my Doom banner, or splash screen icon. I’d also like to change it from time to time, but I like this dodecahedron well enough.
#+BEGIN_SRC elisp
(setq +doom-dashboard-banner-dir "~/.doom.d/banner"
      my/banner-template-file "dodecahedron.svg"
      +doom-dashboard-banner-file "banner.svg")
#+END_SRC

A fun little trick and terrible hack to recolor the banner. Maybe I should try ~async-shell-command~ rather than ~shell-command~? We can even set an override color on a per-theme basis.
#+BEGIN_SRC elisp
;; TODO I think I'm probably supposed to ~defvar~ these so they're properly
;; documented.
(setq my/theme-banner-color-overrides
      '((doom-gruvbox-light . red)
        (poet . yellow))
      my/banner-color-default 'blue)

(defun my/theme-banner-color ()
  "Get the preferred banner color for the current `doom-theme` as a hex code."
    (let* ((pair (seq-find
                 (lambda (pair) (eq (car pair) doom-theme))
                 my/theme-banner-color-overrides))
          (color (if pair (cdr pair) my/banner-color-default)))
      (my/theme-color color)))

(defun my/recolor-banner ()
  "Recolor the banner image to the preferred color for the current theme."
  (shell-command
   (concat "sed -e \""
           ;; replace black with the banner color
           "s/#000000/" (my/theme-banner-color) "/g" "; "
           ;; replace white with background color
           "s/#FFFFFF/" (my/theme-color 'bg) "/g" "\" "
           +doom-dashboard-banner-dir "/" my/banner-template-file " > "
           +doom-dashboard-banner-dir "/" +doom-dashboard-banner-file)))
#+END_SRC

Let’s run this when we change themes, so it’s colored correctly.
#+BEGIN_SRC elisp
(add-hook 'doom-load-theme-hook #'my/recolor-banner)
#+END_SRC

* Fonts, faces, and characters
** Better color defaults
Fix some pretty awful ~lsp~ colors. To check: are all these ~after!~ macros expensive? Are they hurting my startup time? Should I be putting them in some specific order?
#+BEGIN_SRC elisp
(after! lsp-modeline
  (after! ewal-doom-themes
    (set-face-attribute 'lsp-modeline-code-actions-face nil
                        :foreground (my/ewal-color 'cyan))))
#+END_SRC

Coq-related face settings from my old Spacemacs config, which are not exactly correct any more.
#+BEGIN_SRC elisp
;; Change the color-setting from the version, wrap in 'set-face-attribute' call
;; (proof-locked-face :background ,(ewal-get-color 'background 2))
#+END_SRC

** Org faces
Some sensible org theme things. I think I snagged these from [[https://github.com/biblecraft-abe/Spacemacs][Abraham Peters]].
#+BEGIN_SRC elisp
 (let* ((base-font-color     (face-foreground 'default nil 'default))
        ;; ~headline~ previously had a ~:foreground ,base-font-color~ in it. But
        ;; this seems not to give the right color--all headlines come out black.
        (headline           `(:inherit default :weight bold)))
   ;; A Doom Emacs macro that provides a simpler syntax for `custom-theme-set-faces`
   (custom-theme-set-faces!
    'user
    ;; Why does 0.8 seem to be about right? Well, it’s the same as the ratio
    ;; between my/code-size and my/text-size. That’s probably a hint as to
    ;; what’s going on.
    `(org-code :family ,my/code-font :height 0.8)
    `(org-block :family ,my/code-font :height 0.8)
    `(org-level-8 ,@headline)
    `(org-level-7 ,@headline)
    `(org-level-6 ,@headline)
    `(org-level-5 ,@headline)
    `(org-level-4 ,@headline :height 1.1)
    `(org-level-3 ,@headline :height 1.25)
    `(org-level-2 ,@headline :height 1.5)
    `(org-level-1 ,@headline :height 1.75)
    `(org-document-title ,@headline :height 2.0 :underline nil)))
#+END_SRC

** Ligatures
Fira Code is a pretty nice programming font, and I like its ligatures and special characters. But some of them, like rendering ~x~ as a multiplication sign, are a little ugly.
#+BEGIN_SRC elisp
(custom-set-variables '(fira-code-mode-disabled-ligatures '("[]" "#{" "#(" "#[" "#_(" "x")))
(add-hook 'prog-mode-hook 'fira-code-mode)
#+END_SRC

* Programming
** Rust
Not necessary if using ~:editor format-all~ (or something like this? I forget what the setting was called), but that seems not to work. In any case, I think I might prefer ~rust-mode~ to ~rustic~. I might consider going back, even within Doom.
#+BEGIN_SRC elisp
(setq rustic-lsp-server 'rust-analyzer
      rustic-format-on-save t)
#+END_SRC

I want to ensure that my compilation buffers show up on the right-hand side. Check the documentation for ~set-popup-rule!~ to find out about all the keys you can use for this.
#+BEGIN_SRC elisp
(after! popup
  (set-popup-rule! "^\\*rustic"
    :select t    ;; focus the window after it opens
    ;; :action
    :side 'right ;; on the rhs of the screen
    :width 0.5   ;; and fill 50% of it.
  )
)
#+END_SRC

It is sometimes advised, I think, to abrogate the use of advice. But I thought it would be much nicer to use this approach here than to define an entirely /new/ function and to bind it to some unused key or to override the binding for  ~rustic-cargo-run~. This is a much simpler version of the function ~cargo-comint-run~  appearing in my Spacemacs config.
#+BEGIN_SRC elisp
(defadvice! cargo-run-comint ()
  "After calling `rustic-cargo-run`, enter comint mode and make the buffer editable."
  :after #'rustic-cargo-run
  (evil-goto-line))
  ;; (sleep-for 1)
  ;;  (comint-mode)
  ;;  (read-only-mode 0)
  ;;  (evil-goto-line)
  ;;  (evil-append-line))
  ;; (read-only-mode 0))
#+END_SRC

** Company
Will this make ~company-mode~ work?
#+BEGIN_SRC elisp
(set-company-backend!
  '(c-mode
    haskell-mode
    emacs-lisp-mode
    lisp-mode
    sh-mode
    python-mode
    rust-mode
    js-mode)
  '(:separate
    ;; company-tabnine
    company-files
    company-yasnippet))

(setq +lsp-company-backend
      '(company-lsp :with company-tabnine :separate))
#+END_SRC

* Org-mode
** Main Org-mode settings
A little helper function for completing latex fragments. Some notes:
+ Newlines aren’t working. It might be that you’re supposed to use a "character class" like [:space:] or \s-, but neither of these
+ ~looking-back~ is known to be quite slow. See [[https://emacs.stackexchange.com/a/12744][this answer]].
#+BEGIN_SRC elisp
(defun my/latex-fragment-completion ()
  ;; This regexp will trigger LaTeX compilation whenever following a '$' character by an optional punctuation character and whitespace.
  (when (looking-back "\$[\.,!?-]?\s")
    (save-excursion
      (backward-char 1)
      (org-toggle-latex-fragment))))
#+END_SRC

All the overridden org-mode variables
#+BEGIN_SRC elisp
(after! org
  (setq-default
   org-log-done 'time  ;; when finishing a task, log the time
   org-hide-emphasis-markers t
   org-startup-with-latex-preview t
   org-agenda-files (directory-files my/org-agenda-directory t "\.org$")
   org-latex-create-formula-image-program 'imagemagick)
  (setq org-format-latex-options (plist-put org-format-latex-options :scale 2.0))
  ;; Specify the extra latex packages we want to use, and include them.
  (setq org-latex-packages-extra '("tikz" "tikz-cd" "physics"))
  ;; This assumes that there was nothing in this variable to begin with!
  (setq org-latex-packages-alist
        (mapcar (lambda (package) `("" ,package t))
              org-latex-packages-extra))
  ;; This will render the bullets more correctly!
  (set-face-attribute 'org-superstar-header-bullet nil :family "Fira Code")
  ;; If this doesn’t render correctly: it’s a beaker
  (push '("research" . "") org-pretty-tags-surrogate-strings)
  ;; If this doesn’t render correctly: it’s a book
  (push '("physical" . "") org-pretty-tags-surrogate-strings)

  ;; Overwrites the app that should be used to open pdfs from Emacs. This was
  ;; originally set to ~default~ in org.el, which uses the "system default pdf
  ;; viewer." You should also figure out how to it figures out what that is, and
  ;; how to change it. There seems to be no builtin for overwriting alists, but
  ;; this is earlier entries shadow later entries, so this has the same effect.
  (add-to-list 'org-file-apps '("\\.pdf\\'" . emacs)))


;; TODO break out a lot of these hooks into some appropriate ‘text-mode’ hook.
(add-hook 'org-mode-hook
          '(lambda ()
             ;; disable flycheck if you're in an agenda file
             (when (string-prefix-p my/org-agenda-directory (buffer-file-name))
               (flycheck-mode -1))
             (visual-line-mode) ;; improved wrapping
             (org-indent-mode)
             (olivetti-mode)
             (electric-quote-mode) ;; replace “ and ”.
             (display-line-numbers-mode -1)
             (org-variable-pitch-minor-mode)
             (org-superstar-mode)
             (org-pretty-tags-mode)
             (add-hook 'post-self-insert-hook #'my/latex-fragment-completion 'append 'local)))
#+END_SRC

** Org-roam
#+BEGIN_SRC elisp
;; org-roam configurations
(after! org-roam
  (setq-default org-roam-directory (concat org-directory "/roam"))
  (after! deft
    (setq-default deft-directory org-roam-directory)))

(add-hook 'org-roam-mode-hook
          '(lambda ()
             ;; This shouldn't be necessary... Is it?
             (org-mode)
             ;; Do I want this in all org buffers?
             (org-zotxt-mode)))
#+END_SRC

I’m also using ~org-roam-server~ to visualize the org graph. So far, these settings are the defaults from the [[https://github.com/org-roam/org-roam-server#Installation][installation instructions]], except that I’ve incremented to port number to something less common, and removed the ~:ensure~ attribute, which Doom Emacs discourages.

#+BEGIN_SRC elisp
(use-package! org-roam-server
  :config
  (setq org-roam-server-host "127.0.0.1"
        org-roam-server-port 8081
        org-roam-server-authenticate nil
        org-roam-server-export-inline-images t
        org-roam-server-serve-files nil
        org-roam-server-served-file-extensions '("pdf" "mp4" "ogv")
        org-roam-server-network-poll t
        org-roam-server-network-arrows nil
        org-roam-server-network-label-truncate t
        org-roam-server-network-label-truncate-length 60
        org-roam-server-network-label-wrap-length 20))
#+END_SRC

** Citation management
This is a great big mess. You really need to figure it out at some point.

Here are some keybindings for common
#+BEGIN_SRC elisp
(map! :after org
      :map org-mode-map
      :localleader
      :prefix ("z" . "org-zotxt")
      "i" #'org-zotxt-insert-reference-link
      "u" #'org-zotxt-update-reference-link-at-point
      "o" #'org-zotxt-open-attachment)
#+END_SRC

#+BEGIN_SRC elisp
;; Should this be ~after!~ something?
;; TODO review this section, see which settings you actually need.
(setq! +biblio-pdf-library-dir (concat org-directory "/biblio/pdfs/")
       +biblio-default-bibliography-files (directory-files
                                           (concat org-directory "/biblio/bibs")
                                           t directory-files-no-dot-files-regexp)
       +biblio-notes-path (concat org-roam-directory "/refs/"))
#+END_SRC

We have to ensure that we open pdfs by looking up the bibliography ~file~ field, rather than by looking in a pdf directory.
#+BEGIN_SRC elisp
(after! org-roam-bibtex
  (setq! org-ref-get-pdf-filename-function 'org-ref-get-pdf-filename-helm-bibtex)
  (setq! orb-persp-project `("notes" . ,+biblio-notes-path)))
#+END_SRC

Now, in order for ~org-ref-open-notes-at-point~ (via ~orb-edit-notes~) to work properly, I need in particular to set some ~orb-templates~ that will put the notes in the right subdirectory and so forth. To get started, I’ve *shamelessly* copied the templates of the illustrious [[https://github.com/zaeph/.emacs.d/blob/0f4bf9a500ef6397b93c41cb51602bea9ab9a4c3/init.el][Leo Vivier]], tweaking them only a little to my tastes.
#+BEGIN_SRC elisp
(defvar my/orb-header
          (concat
           "#+title: ${title}\n"
           "#+author: ${author-or-editor}")
  "Format of the header to use for `orb-templates'.")

(setq! orb-templates
       `(("r" "ref" plain
          (function org-roam-capture--get-point)
          ""
          :file-name "refs/${slug}"
          :head ,(concat
                  my/orb-header "\n"
                  "#+roam_key: ${ref}\n"
                  "#+created: %U\n"
                  "#+last_modified: %U\n\n")
          :unnarrowed t)
         ("p" "ref + physical" plain
          (function org-roam-capture--get-point)
          ""
          :file-name "refs/${slug}"
          :head ,(concat
                  my/orb-header "\n"
                  "#+roam_key: ${ref}\n"
                  "\n"
                  "* Notes :physical:\n"))
         ("n" "ref + noter" plain
          (function org-roam-capture--get-point)
          ""
          :file-name "refs/${slug}"
          :head ,(concat
                  my/orb-header "\n"
                  "#+roam_key: ${ref}\n\n"
                  "* Notes :noter:\n"
                  ":PROPERTIES:\n"
                  ":NOTER_DOCUMENT: %(orb-process-file-field \"${=key=}\")\n"
                  ":NOTER_PAGE:\n"
                  ":END:\n"))))
#+END_SRC

** Org-journal
#+BEGIN_SRC elisp
;; org-journal configurations
(after! org-journal
  (setq-default org-journal-dir (concat org-directory "/journal")
                org-journal-encrypt-journal t
                org-journal-file-type 'weekly))
#+END_SRC

* Documents
Some pdf viewing preferences: use the ~ewal~ colors, and start in ~midnight-minor-mode~.
#+BEGIN_SRC elisp
(defun my/set-pdf-view-midnight-colors ()
  "Sets the colors for viewing pdfs in 'dark mode'"
  (interactive)
  (setq pdf-view-midnight-colors
        `(,(my/theme-color 'fg) .
          ,(my/theme-color 'bg))))

;; TODO add a load-theme hook for this

;; Does the framework allow this kind of nesting of these macros?
(after! pdf
  (after! ewal-doom-themes
    ;; This should be a cons (FOREGROUND . BACKGROUND) of colors.
    (setq pdf-view-midnight-colors
          `(,(my/ewal-color 'foreground) .
            ,(my/ewal-color 'background)))))

;; Start in midnight mode!
(add-hook 'pdf-view-mode-hook #'pdf-view-midnight-minor-mode)
#+END_SRC

* Email
Here are the public parts of my email configuration. First, we’ll import the bits that I want to keep private.
#+BEGIN_SRC elisp
(after! mu4e
  (load! "./mu4e-config.el"))
#+END_SRC

These settings specify how to retrieve mail.
#+BEGIN_SRC elisp
(setq
 +mu4e-backend 'offlineimap
 mu4e-get-mail-command "offlineimap"
 mu4e-context-policy 'pick-first
 message-kill-buffer-on-exit t
 ;; should just quit when I type `q`
 mu4e-confirm-quit nil)
#+END_SRC

And these are just setting up usual text-viewing minor modes. I wonder if I should fold these into ~text-mode~, since they’re redundant with some of the ~org-mode~ minor modes.
#+BEGIN_SRC elisp
;; TODO are line breaks still not handled correctly?
(add-hook 'mu4e-view-mode-hook 'olivetti-mode)
(add-hook 'mu4e-compose-mode-hook
          '(lambda ()
             (visual-line-mode) ;; improved wrapping
             (olivetti-mode)
             (display-line-numbers-mode -1)
             (use-hard-newlines -1)
             (flyspell-mode))
          t)
#+END_SRC

* Technical drawing
I’d like to be able to run a single command and add or update an ~.svg~ in-place. Here are some little commands to do this, inspired by [[https://castel.dev/post/lecture-notes-2/][Gilles Castel]], whose scripts I’m shamelessly using.

#+BEGIN_SRC elisp
(defun my/figure-dir ()
  "Return the directory where figures are saved"
  (concat default-directory "figures/"))

(defun my/inkscape-figures ()
  "Creates a new figure in Inkscape"
  (interactive)
  (let*
      ((no-dots directory-files-no-dot-files-regexp)
       (figure-dir (my/figure-dir))
       (figure-name
        ;; get the figure name by asking the user for it
        (progn
          ;; make the figure directory if it doesn’t exist
          (if (not (file-exists-p figure-dir))
              (mkdir figure-dir))
          (concat
           ;; query the user for a figure name
           (completing-read
            "Figure name: "
            (mapcar #'file-name-nondirectory
             (mapcar #'file-name-sans-extension
                   (directory-files figure-dir t no-dots))))))))

    (if (file-exists-p (concat figure-dir figure-name))
      ;; run the command to edit the figure
      (shell-command
       (concat "inkscape-figures edit " figure-dir figure-name))
      ;; otherwise, run the command to create the figure
      (shell-command
       (concat "inkscape-figures create " figure-dir figure-name)))

    ;; return the figure name
    figure-name))
#+END_SRC

I’d also like to provide some convenience functions for automatically inserting these in various text modes. This is a work in progress.
#+BEGIN_SRC elisp
(defun my/inkscape-figures-insert ()
  "Create or edit and insert in inkscape figure in an Org buffer"
  (interactive)
  (let ((figure-name (my/inkscape-figures)))
    (insert (concat "[[" (my/figure-dir) figure-name ".svg]]"))))
#+END_SRC

* Odds and ends
A fun little thing you can do in Emacs 27. Function due to an internet stranger, whom I should cite if I ever find them again.
#+BEGIN_SRC elisp
(defun screenshot-svg ()
  "Save a screenshot of the current frame as an SVG image.
Saves to a temp file and puts the filename in the kill ring."
  (interactive)
  (let* ((filename (make-temp-file "Emacs" nil ".svg"))
         (data (x-export-frames nil 'svg)))
    (with-temp-file filename
      (insert data))
    (kill-new filename)
    (message filename)))
#+END_SRC

I’d like my dictionary popups to fill a little more of the screen than they do by default.
#+BEGIN_SRC elisp
(set-popup-rule! "^\\*WordNut"
  ;; :action
  :height 0.3   ;; fill this much of the screen
  )
#+END_SRC
** Notifications
#+BEGIN_SRC elisp
;; alert configurations
(after! alert
  (setq-default alert-default-style 'libnotify
                ;; alerts fade out after 30 seconds
                alert-fade-time 30))

(add-hook 'after-init-hook 'org-wild-notifier-mode)
#+END_SRC

#  LocalWords:  config namespacing
